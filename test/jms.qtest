#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%require-types
%strict-args
%enable-all-warnings

%requires jni
%requires QUnit

%exec-class Main

class ClassLoader {

    private:internal {
        Jni::Object this;
        Jni::Method loadClassMethod;
    }

    constructor(softlist urls) {
        Jni::Class urlClass = Jni::load_class("java/net/URL");
        Jni::Constructor urlCtor = urlClass.getConstructor("(Ljava/lang/String;)V");

        Jni::Array urlArray = Jni::new_object_array(urls.size(), urlClass);
        foreach string url in (urls) {
            urlArray.set($#, urlCtor.newInstance(url));
        }

        Jni::Class classLoaderClass = Jni::load_class("java/net/URLClassLoader");

        loadClassMethod = classLoaderClass.getMethod("loadClass", "(Ljava/lang/String;)Ljava/lang/Class;");
        this = classLoaderClass.getConstructor("([Ljava/net/URL;)V").newInstance(urlArray);
    }

    Jni::Class loadClass(string name) {
        name =~ tr/\//\./;
        return loadClassMethod.invoke(this, name);
    }

    Jni::Object get() {
        return this;
    }
}

class Message {

    private {
        Jni::Object this;
    }

    constructor(Jni::Object this) {
        self.this = this;
    }

    Jni::Object get() {
        return this;
    }
}

class TextMessage inherits Message {

    private:internal {
        Jni::Method setTextMethod;
        Jni::Method getTextMethod;
    }

    constructor(Jni::Object this) : Message(this) {
        Jni::Class clazz = this.getClass();
        setTextMethod = clazz.getMethod("setText", "(Ljava/lang/String;)V");
        getTextMethod = clazz.getMethod("getText", "()Ljava/lang/String;");
    }

    setText(string text) {
        setTextMethod.invoke(this, text);
    }

    string getText() {
        return getTextMethod.invoke(this);
    }
}

class MessageProducer {

    private:internal {
        Jni::Object this;
        Jni::Method sendMethod;
    }

    constructor(Jni::Object this) {
        self.this = this;
        Jni::Class clazz = this.getClass();
        sendMethod = clazz.getMethod("send", "(Ljavax/jms/Message;)V");
    }

    send(Message msg) {
        sendMethod.invoke(this, msg.get());
    }
}

# workaround for issue 1303 - this should be directly in MessageConsumer::setMessageListener
code sub wrapListener(code listener, Jni::Class textMessageClass) {
    return sub(Jni::Method method, *Jni::Array args) {
        Jni::Object o = args.get(0);
        Message m;
        if (textMessageClass.isInstance(o)) {
            m = new TextMessage(o);
        } else {
            m = new Message(o);
        }
        listener(m);
    };
}

class MessageConsumer {

    private:internal {
        ClassLoader classLoader;
        Jni::Object this;
        Jni::Method setMessageListenerMethod;
        Jni::Class textMessageClass;
        Jni::Class messageListenerClass;
    }

    constructor(ClassLoader classLoader, Jni::Object this) {
        self.classLoader = classLoader;
        self.this = this;
        Jni::Class clazz = this.getClass();
        setMessageListenerMethod = clazz.getMethod("setMessageListener", "(Ljavax/jms/MessageListener;)V");
        textMessageClass = classLoader.loadClass("javax/jms/TextMessage");
        messageListenerClass = classLoader.loadClass("javax/jms/MessageListener");
    }

    setMessageListener(code listener) {
        code w = wrapListener(listener, textMessageClass);
        setMessageListenerMethod.invoke(this, Jni::implement_interface(classLoader.get(), new Jni::InvocationHandler(w), messageListenerClass));
    }
}

class Destination {

    private:internal {
        Jni::Object this;
    }

    constructor(Jni::Object this) {
        self.this = this;
    }

    Jni::Object get() {
        return this;
    }
}

class Session {

    public {
        const AUTO_ACKNOWLEDGE = 1;
        const CLIENT_ACKNOWLEDGE = 2;
        const DUPS_OK_ACKNOWLEDGE = 3;
        const SESSION_TRANSACTED = 0;
    }

    private:internal {
        ClassLoader classLoader;
        Jni::Object this;
        Jni::Method createProducerMethod;
        Jni::Method createConsumerMethod;
        Jni::Method createTextMessageMethod;
    }

    constructor(ClassLoader classLoader, Jni::Object this) {
        self.classLoader = classLoader;
        self.this = this;
        Jni::Class clazz = this.getClass();
        createProducerMethod = clazz.getMethod("createProducer", "(Ljavax/jms/Destination;)Ljavax/jms/MessageProducer;");
        createConsumerMethod = clazz.getMethod("createConsumer", "(Ljavax/jms/Destination;)Ljavax/jms/MessageConsumer;");
        createTextMessageMethod = clazz.getMethod("createTextMessage", "()Ljavax/jms/TextMessage;");
    }

    MessageProducer createProducer(Destination dest) {
        return new MessageProducer(createProducerMethod.invoke(this, dest.get()));
    }

    MessageConsumer createConsumer(Destination dest) {
        return new MessageConsumer(classLoader, createConsumerMethod.invoke(this, dest.get()));
    }

    TextMessage createTextMessage() {
        return new TextMessage(createTextMessageMethod.invoke(this));
    }
}

class Connection {

    private:internal {
        ClassLoader classLoader;
        Jni::Object this;
        Jni::Method closeMethod;
        Jni::Method startMethod;
        Jni::Method createSessionMethod;
    }

    constructor(ClassLoader classLoader, Jni::Object this) {
        self.classLoader = classLoader;
        self.this = this;
        Jni::Class clazz = this.getClass();
        closeMethod = clazz.getMethod("close", "()V");
        startMethod = clazz.getMethod("start", "()V");
        createSessionMethod = clazz.getMethod("createSession", "(I)Ljavax/jms/Session;");
    }

    destructor() {
        close();
    }

    close() {
        if (this) {
            closeMethod.invoke(this);
            delete this;
        }
    }

    Session createSession(int sessionMode) {
        return new Session(classLoader, createSessionMethod.invoke(this, sessionMode));
    }

    start() {
        startMethod.invoke(this);
    }
}

class ConnectionFactory {

    private:internal {
        ClassLoader classLoader;
        Jni::Object this;
        Jni::Method createConnectionMethod;
    }

    constructor(ClassLoader classLoader, Jni::Object this) {
        self.classLoader = classLoader;
        self.this = this;
        createConnectionMethod = this.getClass().getMethod("createConnection", "()Ljavax/jms/Connection;");
    }

    Connection createConnection() {
        return new Connection(classLoader, createConnectionMethod.invoke(this));
    }
}

class JmsContext {

    private:internal {
        ClassLoader classLoader;
        Jni::Class ctxClass;
        Jni::Method lookup;
        Jni::Object namingContext;
    }

    constructor(softlist classpath) {
        classLoader = new ClassLoader(classpath);
        ctxClass = Jni::load_class("javax/naming/InitialContext");
        lookup = ctxClass.getMethod("lookup", "(Ljava/lang/String;)Ljava/lang/Object;");

        Jni::Class threadClass = Jni::load_class("java/lang/Thread");
        Jni::Object thread = threadClass.getStaticMethod("currentThread", "()Ljava/lang/Thread;").invoke();
        threadClass.getMethod("setContextClassLoader", "(Ljava/lang/ClassLoader;)V").invoke(thread, classLoader.get());

        namingContext = ctxClass.getConstructor("()V").newInstance();
    }

    ConnectionFactory getConnectionFactory(string name) {
        return new ConnectionFactory(classLoader, lookup.invoke(namingContext, name));
    }

    Destination getDestination(string name) {
        return new Destination(lookup.invoke(namingContext, name));

    }
}

public class Main inherits QUnit::Test {

    constructor() : Test("jni test", "1.0") {
        addTestCase("base test", \testJms());

        # Return for compatibility with test harness that checks return value.
        set_return_value(main());
    }

    testJms() {
        JmsContext ctx("file:///home/otethal/glassfish4/glassfish/lib/gf-client.jar");

        ConnectionFactory connectionFactory = ctx.getConnectionFactory("jms/__defaultConnectionFactory");
        Connection connection = connectionFactory.createConnection();
        Session session = connection.createSession(Session::AUTO_ACKNOWLEDGE);

        Destination queue = ctx.getDestination("abc");

        MessageProducer producer = session.createProducer(queue);
        MessageConsumer consumer = session.createConsumer(queue);

        consumer.setMessageListener(sub(object msg) {
            printf("IN CALLBACK\n");
            printf("Received: %s\n", msg.getText());
            printf("END CALLBACK\n");
        });

        connection.start();

        TextMessage message = session.createTextMessage();
        message.setText("Hello, world!");

        producer.send(message);
    }
}
